//  Calculation of π using quadrature using the Java programming language.
//
//  Copyright © 2013–2017  Russel Winder

apply plugin: 'java'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.jcsp:jcsp:1.1-rc5'
    compile 'org.codehaus.gpars:gpars:1.+'
    compile 'org.codehaus.groovy:groovy-all:2.+'
    compile 'org.functionaljava:functionaljava-java8:4.+'
}

sourceCompatibility = 8

final targets = new File("${projectDir}/src/main/java/uk/org/winder/pi_quadrature").listFiles({it.name.startsWith('Pi_') && it.name.endsWith('.java')} as FileFilter).collect{ file ->
    final root = file.name.replaceFirst(/\.java$/, '')
    final targetName = 'run_' + root
    task (targetName, type: JavaExec, dependsOn: 'classes') {
        main = 'uk.org.winder.pi_quadrature.' + root
        classpath = sourceSets.main.runtimeClasspath
    }
    targetName
}

task runAll(dependsOn: targets)

defaultTasks 'classes'
