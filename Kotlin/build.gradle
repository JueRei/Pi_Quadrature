buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.+"
  }
}

apply plugin: 'kotlin'

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:1.+"
}

ext.targets = []
new File("${projectDir}/src/main/kotlin/uk/org/winder/pi_quadrature").eachFileMatch(~/pi_.*\.kt/) { filename ->
  final root = filename.name.replaceFirst(/\.kt$/, '')
  final className = 'uk.org.winder.pi_quadrature.' + root.capitalize() + "Kt"
  targets << 'run_' + root
  task ('run_' + root, type: JavaExec, dependsOn: 'classes') {
    main = className
    classpath = sourceSets.main.runtimeClasspath
  }
}

task runAll(dependsOn: ext.targets)

sourceSets {
  main.java.srcDirs += 'src/main/kotlin'
}

defaultTasks 'runAll'
