buildscript {
  ext.kotlin_version = '1.0.0'
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'eclipse'

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

new File("${projectDir}/src/main/kotlin/uk/org/winder/pi_quadrature").eachFileMatch(~/pi_.*\.kt/) { filename ->
  final root = filename.name.replaceFirst(/\.kt$/, '')
  final className = 'uk.org.winder.pi_quadrature.' + root.capitalize() + "Kt"
  task ('run_' + root, type: JavaExec, dependsOn: 'classes') {
    main = className
    classpath = sourceSets.main.runtimeClasspath
  }
}

task wrapper(type:Wrapper) {
  gradleVersion = '2.11'
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin'
}

defaultTasks 'classes'
