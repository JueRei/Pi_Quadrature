# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2008–2013, 2016  Russel Winder

import os
import subprocess
import sys

sys.path.append('../site_scons')

from executablesupport import compileTargets, addCompileTarget, runTargets, addRunTarget, createHelp

chapelEnvironment = Environment(ENV=os.environ)

for item in Glob('pi_*.chpl'):
    addRunTarget(chapelEnvironment,
                 addCompileTarget(chapelEnvironment.Command(item.name.replace('.chpl', ''), [item, 'output.chpl'], 'chpl -O --fast -o $TARGET $SOURCES')))
    chapelEnvironment.SideEffect(item.name.replace('.chpl', '_real'), item.name.replace('.chpl', ''))


def runAllTargets(target, source, env):
    for s in source:
        print('###################################### ' + s.name)
        assert 0 == subprocess.call((s.name,))


Command('runAll', compileTargets, runAllTargets)

Default(compileTargets)

Help(createHelp())
