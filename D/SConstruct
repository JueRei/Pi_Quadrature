# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature using the D programming language.
#
#  Copyright © 2008–2017  Russel Winder

import os
import sys
sys.path.append('../site_scons')

from executablesupport import compileTargets, runTargets, addCompileTarget, addRunTarget, createHelp

dEnvironment = {
    'dmd': Environment(
        tools=['dmd', 'link'],
        DFLAGS=['-O', '-release'],
    ),
    'gdc':  Environment(
        tools=['gdc', 'link'],
        DFLAGS=['-O3'],
    ),
    'ldc': Environment(
        tools=['ldc', 'link'],
        DFLAGS=['-O', '-release'],
    ),
#}['dmd']
#}['gdc']
}['ldc']

outputFunctions = dEnvironment.Object('outputFunctions.d')

for item in Glob('pi_*.d'):
    root = os.path.splitext(item.name)[0]
    addRunTarget(dEnvironment, addCompileTarget(dEnvironment.ProgramAllAtOnce([item, outputFunctions])))

Alias('runAll', runTargets)

Default(compileTargets)

Help(createHelp())
