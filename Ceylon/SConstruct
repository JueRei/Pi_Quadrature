# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2013–2014, 2016  Russel Winder

import os
import sys

ceylonEnvironment = Environment(
    # Needed as the Ceylon script requires tput and hence $TERM – affects the rendering of Unicode
    # codepoints.
    ENV=os.environ,
)

version = '1.0.0'

sourceDirectory = 'source'
moduleName = 'uk.org.winder.pi_quadrature'

moduleSourceDirectory = sourceDirectory + '/' + moduleName.replace('.', '/')


sourceFiles = Glob(moduleSourceDirectory + '/*.ceylon') + Glob(moduleSourceDirectory + '/*/*.ceylon')

modulesCarDirectory = 'modules/' + moduleName.replace('.', '/') + '/' + version

moduleRoot = modulesCarDirectory + '/' + moduleName + '-' + version

carFile = ceylonEnvironment.Command(moduleRoot + '.car', sourceFiles, 'ceylon compile {}'.format(moduleName))
SideEffect([moduleRoot + extension for extension in ('car.sha1', '.src', '.src.sha1')], carFile)

Alias('compile', carFile)

runTargets = [
    ceylonEnvironment.Command('run_' + package, carFile,  'ceylon run --run={}.run {}'.format(package, moduleName))
    for package in [package.path.replace(sourceDirectory + '/', '').replace('/', '.') for package in Glob(moduleSourceDirectory + '/pi_*')]
]

Alias('runAll', runTargets)

Default('compile')

Clean('.', ['modules'])

Help('''
compile (default)
runAll

''' + '\n'.join(t[0].name for t in runTargets) + '\n'
)
