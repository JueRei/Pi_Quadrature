plugins {
  id "org.frege-lang" version "0.8"
}

targetCompatibility = 8
//targetCompatibility = 9

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  //compile 'org.frege-lang:frege:3.24+'
  compile files(System.properties['user.home'] + '/lib/JDK/frege.jar')
}

final targets = new File("${projectDir}/src/main/frege").listFiles({it.name.startsWith('pi_') && it.name.endsWith('.fr')} as FileFilter).collect{ file ->
  final className = file.name.replaceFirst(/\.fr$/, '').replaceFirst(/^p/, 'P').replaceAll(/_([a-z])/){a, b -> "_${b.toUpperCase()}"}
  final targetName = 'run_' + className
  task (targetName, type: JavaExec, dependsOn: 'classes') {
    main = className
    classpath = sourceSets.main.runtimeClasspath
  }
  targetName
}

task runAll(dependsOn: targets)

defaultTasks 'classes'
