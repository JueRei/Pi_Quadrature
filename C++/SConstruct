# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2008–2013 Russel Winder

import os
import re
import sys
sys.path.append('../site_scons')

from executablesupport import compileTargets, addCompileTarget, addRunTarget, createHelp, libraryPath

extraLibName = libraryPath()

cppFlags = ['-std=c++14', '-O3', '-Wall', '-Wextra']

cppEnvironment = Environment(
    tools=['g++', 'gnulink'],
    ENV=os.environ,
)

# Fedora Rawhide packaging does not put symbolic links to the executables into /usr/bin, so we must add the
# /usr/lib64/openmpi/bin directory to the path. Similarly /usr/lib64/openmpi/lib is where the libraries are.
fedoraOpenMPIDirectory = '/usr/lib64/openmpi'
fedoraOpenMPIBinDirectory = fedoraOpenMPIDirectory + '/bin'
fedoraOpenMPILibDirectory = fedoraOpenMPIDirectory + '/lib'
if os.path.exists(fedoraOpenMPIBinDirectory):
    cppEnvironment['ENV']['PATH'] += ':{}'.format(fedoraOpenMPIBinDirectory)

output = cppEnvironment.Object('output.cpp', CXXFLAGS=cppFlags)

def cppRule(globPattern, compiler='g++', cpppath=[], cxxflags=cppFlags, linkflags=[], libpath=[], libs=[]):
    for item in Glob(globPattern):
        addRunTarget(cppEnvironment,
            addCompileTarget(
                cppEnvironment.Program(
                    os.path.splitext(item.name)[0], [item.name, output],
                    CXX=compiler, CPPPATH=cpppath, CXXFLAGS=cxxflags, LINKFLAGS=['-std=c++14'] + linkflags, LIBPATH=libpath, LIBS=libs)))

cppRule('pi_sequential_*.cpp')
cppRule('pi_mpi*.cpp', compiler='mpic++')  # This MPI execution target runs things sequentially.  Use the command "mpirun -np N" to run the code on N processors.
cppRule('pi_boostMPI*.cpp', compiler='mpic++', libpath=['-L'+fedoraOpenMPILibDirectory] if os.path.exists(fedoraOpenMPILibDirectory) else [], libs=['boost_mpi'])  # This MPI execution target runs things sequentially.  Use the command "mpirun -np N" to run the code on N processors.
cppRule('pi_parallel_thread*.cpp', libs=['pthread'])
cppRule('pi_parallel_boostThread*.cpp', libs=['boost_thread', 'boost_system'])
cppRule('pi_parallel_futures*.cpp', libs=['pthread'])
cppRule('pi_parallel_tbb*.cpp', libs=['tbb'])
cppRule('pi_parallel_openmp*.cpp', cxxflags=cppFlags + ['-fopenmp'], libs=['gomp'])
#cppRule('pi_parallel_cppcsp2.cpp', cpppath=[os.environ['HOME'] + '/include'], libpath=[os.environ['HOME'] + '/Built/lib'], libs=['cppcsp2', 'boost_system', 'pthread'])
#cppRule('pi_parallel_justThread*.cpp', cpppath=[extraLibName + '/JustThreadPro/include'], linkflags=['--static'], libpath=[extraLibName + '/JustThreadPro/libs'], libs=['justthread', 'pthread'])

Default(compileTargets)

Help(createHelp())
